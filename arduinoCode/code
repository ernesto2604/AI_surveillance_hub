#include "WiFiS3.h"
#include "secrets.h"

const char* ssid = WIFI_SSID;
const char* pass = WIFI_PASS;
const char* server = PI_SERVER_IP;
const char* deviceKey = DEVICE_KEY;
const int piPort = PI_SERVER_PORT;

const int trigPin = 9; 
const int echoPin = 10;  
const int distanceThreshold = 20; 

bool objectInRange = false;
WiFiClient client;

void setup() {
  Serial.begin(9600);
  pinMode(trigPin, OUTPUT);
  pinMode(echoPin, INPUT);

  WiFi.begin(ssid, pass);
  while (WiFi.status() != WL_CONNECTED) {
    delay(1000);
  }
}

void loop() {
  long duration;
  int distance;

  digitalWrite(trigPin, LOW);
  delayMicroseconds(2);
  digitalWrite(trigPin, HIGH);
  delayMicroseconds(10);
  digitalWrite(trigPin, LOW);

  duration = pulseIn(echoPin, HIGH);
  distance = duration * 0.034 / 2;

  Serial.print("Current Distance: ");
  Serial.print(distance);
  Serial.println(" cm");

  if (distance > 0 && distance < distanceThreshold) {
    if (!objectInRange) { 
      Serial.println("**** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** ***");
      Serial.println(">> TARGET DETECTED: Object within 20cm!");
      Serial.println(">> M2M ACTION: Sending HTTP GET to Raspberry Pi...");
      sendAlert();
      objectInRange = true; 
    }
  } else {
    if (objectInRange) {
      objectInRange = false; 
    }
  }
  delay(500); 
}

void sendAlert() {
  if (client.connect(server, piPort)) {
    client.print("GET /detect HTTP/1.1\r\n");
    client.print("Host: ");
    client.print(server);
    client.print("\r\n");
    client.print("X-Device-Key: ");
    client.print(deviceKey);
    client.print("\r\n");
    client.print("Connection: close\r\n\r\n");
    client.stop();
    Serial.print("[SUCCESS] M2M Packet delivered to ");
    Serial.print(server);
    Serial.print(":");
    Serial.println(piPort);
    Serial.println("**** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** ***");
  }
}
